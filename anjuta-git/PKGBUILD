# Maintainer: Emanuel Fernandes <efernandes@tektorque.com>

pkgname=anjuta-git
pkgver=3.11.4.r29.g603930b
pkgrel=1
pkgdesc="GNOME Integrated Development Environment (IDE)"
arch=(i686 x86_64)
url="http://www.anjuta.org/"
license=('GPL')
groups=('gnome-extra')
depends=('gdl' 'vte3' 'autogen' 'devhelp' 'glade' 'libgda' 'subversion' 'neon' 'gnome-icon-theme' 'vala' 'dconf')
makedepends=(git vte3 autogen devhelp glade libgda subversion neon gnome-icon-theme vala dconf gtk-doc yelp-tools gnome-common intltool gobject-introspection itstool gjs python2)
provides=('anjuta' 'libanjuta')
conflicts=('anjuta' 'libanjuta')
#replaces=()
#backup=()
options=('!emptydirs')
install=anjuta.install
source=("$pkgname"::'git+https://git.gnome.org/browse/anjuta'
        'anjuta.install')
#noextract=()
md5sums=('SKIP'
         '2bd3a4ca205859e71dae90e2bbe043ab')

pkgver() {
  cd "$srcdir/$pkgname"
  git describe --long | sed -r 's/^ANJUTA_//;s/([^-]*-g)/r\1/;s/-/./g;s/_/./g'
}

build() {
  cd "$srcdir/$pkgname"
  
  ./autogen.sh --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --enable-glade-catalog --enable-compile-warnings=minimum \
      PYTHON=/usr/bin/python2
  make
}

package() {
  cd "$srcdir/$pkgname"
  make DESTDIR="$pkgdir/" install
}

# vim:set ts=2 sw=2 et:
