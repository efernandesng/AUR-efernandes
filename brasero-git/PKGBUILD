# Maintainer: Mauro Fruet <maurofruet@gmail.com>
# Contributor: Hussam Al-Tayeb <ht990332@gmail.com>

pkgname=brasero-git
pkgver=20121028
pkgrel=1
pkgdesc="A disc burning application for Gnome"
url="http://www.gnome.org/projects/brasero/"
license=('GPL')
depends=('gtk3' 'gst-plugins-base' 'totem-plparser' 'cdrkit' 'cdrdao'
'desktop-file-utils' 'hicolor-icon-theme' 'shared-mime-info' 'libcanberra'
'dvd+rw-tools' 'dconf' 'libsm' 'libtracker-sparql')
makedepends=('intltool' 'pkgconfig' 'git' 'gnome-common' 'gnome-doc-utils')
optdepends=('libburn: alternative back-end'
             'libisofs: libburn back-end'
             'dvdauthor: video project'
             'gst-plugins-bad: video project'
             'vcdimager: video project')
arch=('i686' 'x86_64')
conflicts=('brasero')
provides=('brasero')
options=('!libtool' '!emptydirs')
install=brasero.install
_gitname="brasero"
_gitroot="git://git.gnome.org/${_gitname}"

build() {
    cd "$srcdir"
    msg "Connecting to GIT server...."

    if [[ -d "$_gitname" ]]; then
        cd "$_gitname" && git pull origin && git submodule init \
            && git submodule update && cd "$srcdir"
        msg "The local files are updated."
    else
        git clone "$_gitroot" "$_gitname"
    fi

    msg "GIT checkout done or server timeout"
    msg "Starting build..."

    rm -rf "$_gitname-build"
    git clone "$_gitname" "$_gitname-build"

    cd "$_gitname-build"

    ./autogen.sh --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
                 --disable-caches \
                 --disable-schemas-compile
    make
}

package() {
    cd "$srcdir/$_gitname-build"
    make DESTDIR="$pkgdir" install
}
